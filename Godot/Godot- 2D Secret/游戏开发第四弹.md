# Stat bonuses and penalties （亏损

下面的代码是在 `BattlerStats.gd` 里的

```python
# A list of all properties that can receive bonuses.
const UPGRADABLE_STATS = [
	"max_health", "max_energy", "attack", "defense","speed" , "hit_chance", "evasion"
]

# The property below stores a list of modifiers for each property listed in
# `UPGRADABLE_STATS`.
# The value of a modifier can be any floating-point value, positive or negative.
var _modifiers := {}


# Initializes keys in the modifiers dict, ensuring they all exist.
func _init() -> void:
	for stat in UPGRADABLE_STATS:
		# For each stat, we create an empty dictionary.
		# Each upgrade will be a unique key-value pair.
		_modifiers[stat] = {}
```

由于需要计算各种各样的 bonus 和 penalties，所以为了避免麻烦（果然就是要懒 XDDD ）就使用了 字典来处理这些数据，每一个字典都有各自*唯一*的*键值对*

```python
# Calculates the final value of a single stat. That is, its based value
# with all modifiers applied.
# We reference a stat property name using a string here and update
# it with the `set()` method.
func _recalculate_and_update(stat: String) -> void:
	# All our property names follow a pattern: the base stat has the
	# same identifier as the final stat with the "base_" prefix.
	var value = get("base_" + stat)
	# We get the array of modifiers corresponding to a stat.
	var modifiers: Array = _modifiers[stat]
	for modifier in modifiers:
		value += modifier
	
	# This line ensures the final stat cannot be negative.
	value = max(value, 0.0)
	# Here's where we assign the value to the stat. For instance,
	# if the `stat` argument is "attack", this is like writing
	# attack = value
	set(stat, value)
```

现在这个一直报错的 function 终于消停了 XDDD

`max()` 返回最大的值，比如 `max(-10, 1)` 返回一

## `_modifiers` 的工作原理

```python
{
	"attack": {
		1: 12.0
		2: -3.0
	},
	"defense": {},
	#...
}
```

要计算 bonus 的 attack point 就拿 12 - 3 = 9

`_recalculate_and_update()` 把额外的攻击点数添加进 `base_attack` 里，并由 `attack` 来存储攻击的总数

---

# Adding and removing modifiers

```python
# Adds a modifier that affects the stat with the given `stat_name` and returns
# its unique key.
func add_modifiers(stat_name: String, value: float) -> int:
	return -1


# Removes a modifier associated with the given `stat_name`.
func remove_modifiers(stat_name: String, id: int) -> void:
	pass


# Find the first unused integer in a stat's modifiers keys.
func _generate_unique_id(stat_name: String) -> int:
	return -1
```

先写出框架再往里面加功能 XDDD

```python
# Adds a modifier that affects the stat with the given `stat_name` and returns
# its unique key.
func add_modifiers(stat_name: String, value: float) -> int:
	# assert 断言， 如果是 true 就是 true，如果是 false 就会生成错误
	assert(stat_name in UPGRADABLE_STATS, "Trying to to add a modifier to nonexistent stats.")
	# We use a function to ensure we generate a unique ID for every stat
	# modifier. You can find it below.
	var id = _generate_unique_id(stat_name)
	# Using the unique ID, we save the modifier's value.
	_modifiers[stat_name][id] = value
	# Every time we add or remove a stat modifier, we need to recalculate its
	# final value.
	_recalculate_and_update(stat_name)
	# Returning the id allows the caller to bind it to a signal. For instance
	# with equpment, to call `remove_modifier()` upon removing the equipment
	return id
```

`assert()` 关键字，具体可以看看 assert()

```python
# Removes a modifier associated with the given `stat_name`.
func remove_modifiers(stat_name: String, id: int) -> void:
	# As above, during development, we want to know if we try to remove a
	# modifier that doesn't exist.
	assert(id in _modifiers[stat_name], "Id %s not found in %s" % [id, _modifiers[stat_name]])
	# Here's why we use dictionaries in `_modifiers`: we can arbitrarily erase
	# keys without affecting others, ensuring our unique IDs always work.
	_modifiers[stat_name].erase(id)
	_recalculate_and_update(stat_name)
```



```python
# Find the first unused integer in a stat's modifiers keys.
func _generate_unique_id(stat_name: String) -> int:
	var keys: Array = _modifiers[stat_name].keys()
	# If there are no keys, we return `0`, which is our first valid unique id.
	# Without existing keys, calling methods like `Array.back()` will trigger an
	# error.
	if keys.empty():
		return 0
	else:
		# We always start from the last key, which will always be the highest
		# number, even if we remove modifiers.
		return keys.back() + 1
```

上面这个函数可能会些看不懂，但不用紧，根据上面字典的原理

```python
{
	"attack": {
		1: 12.0
		2: -3.0
	},
	"defense": {},
	#...
}
```

如果 `_modifiers[stat_name].keys` 为空，就代表是没有东西（没有键，就像这样

```python
{
	"attack": {
		12.0
	},
	# ...
}
```

那就返回 0，作为唯一的有效的值

```python
{
	"attack": {
		0: 12.0
	},
	# ...
}
```

如果是字典中已经有其他的键值对了，那就往这个的 keys 上面加个一，确保字典唯一的 id 能够生效

