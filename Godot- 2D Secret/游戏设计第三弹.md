2023-3-19

# Taking turns 
终于到了整个 project 最复杂的部分了。。

```java
func _on_Battler_ready_to_act(battler: Battler) -> void:
    _play_turn(battler)
```

用来探测哪一个 Battler 已经 ready 了

```java
func _play_turn(battler: Battler) -> void:
    var action_data: ActionData
    var targets := []
```

`_play_turn` 是整个 project 最复杂的 method，用来要求每一个 Battler 决定一个 action 和 target。这个 method 有两个分支，分别是：
1.  如果 Battler 是玩家操控的，那么就让玩家使用 menu 来选择 action 和要攻击的 target
2.  如果是 AI 控制的，那么就让 AI 来决定 action 和 target

```python
battler.stats.energy += 1
	# The code below makes a list of selectable targets using `Battler.is_selectable`
	var potential_targets := []
	var opponets := _opponents if battler.is_party_member else _party_mmebers
	for opponet in opponets:
		if opponet.is_selectable:
			potential_targets.append(opponet)
```

第一件事情就是要增加 Battler 的能量（以后可以放大招 XDDD 

之后再创建一个 `potential_targets` 列表，储存能被选择的 Battler （用 `Battler.is_selectable` method

### 第一个分支 （玩家控制的 Battler）

```python
if battler.is_player_controlled():
        # We'll use the selection in the next lesson to move playable battlers
        # forward. This value will also make the Heads-Up Display (HUD) for this
        # battler move forward.
        battler.is_selected = true
        # This line slows down time while the player selects an action and
        # target. The function `set_time_scale()` recursively assigns that value
        # to all characters on the battlefield.
        set_time_scale(0.05)

        # Here is the meat of the player's turn. We use a while loop to wait for
        # the player to select a valid action and target(s).
        #
        # For now, we have two boilerplate asynchronous functions,
        # `_player_select_action_async()` and `_player_select_targets_async()`,
        # that respectively return an action to perform and an array of targets.
        # This seemingly complex setup will allow the player to cancel
        # operations in menus.
        var is_selection_complete := false
        # The loop keeps running until the player selected an action and target.
        while not is_selection_complete:
            # The player has to first select an action, then a target.
            # We store the selected action in the `action_data` variable defined
            # at the start of the function.
            action_data = yield(_player_select_action_async(battler), "completed")
            # If an action applies an effect to the battler only, we
            # automatically set it as the target.
            if action_data.is_targeting_self:
                targets = [battler]
            else:
                targets = yield(
                    _player_select_targets_async(action_data, potential_targets), "completed"
                )
            # If the player selected a correct action and target, we can break
            # out of the loop. I'm using a variable here to make the code
            # readable and clear. You could write a `while true` loop and use
            # the break keyword instead, but doing so makes the code less
            # explicit.
            is_selection_complete = action_data != null && targets != []
        # The player-controlled battler is ready to act. We reset the time scale
        # and deselect the battler.
        set_time_scale(1.0)
        battler.is_selected = false
```

>#  yield() method
> 
> yield(object: Object = null, signal: String = "")
> 
> Stops the function execution and returns the current suspended state to the calling function.


有点长哈 XDDD 但不用紧，有注释可以读 XDD

### 第二个分支 （AI 控制的分支）

```python
    else:
        action_data = battler.actions[0]
        targets = [potential_targets[0]]
```

这两个分支的代码量真的没得比啊 XDD

# Character base stats (玩家的状态)

## Health and energy

```python
# Stores and manages the battler's base stats like health, energy, and base
# damage.
extends Resource
class_name BattlerStats
```

创建一个 script，并继承 Resource，提高复用性

```python
# Emitted when a character has no `health` left.
signal health_depleted
# Emitted every time the value of `health` changes.
# We will use it to animate the life bar.
signal health_changed(old_value, new_value)
# Same as above, but for the `energy`.
signal energy_changed(old_value, new_value)

# The battler's maximum health.
export var max_health := 100.0
export var max_energy := 6
# Note that due to how Resources work, in Godot 3.2, health will not have a
# value of `max_health`. This is why we have the function `reinitialize()`
# below. Each battler should call it when the encounter starts.
# This happens because a resource's initialization happens when you create it 
# in the editor to serialize it, not when you load it in the game.
var health := max_health setget set_health
var energy := 0 setget set_energy

func reinitialize() -> void:
    set_health(max_health)

func set_health(value: float) -> void:
    var health_previous := health
    # We use `clamp()` to ensure the value is always in the [0.0, max_health]
    # interval.
    health = clamp(value, 0.0, max_health)
    emit_signal("health_changed", health_previous, health)
    # As we are working with decimal values, using the `==` operator for
    # comparisons isn't safe. Instead, we need to call `is_equal_approx()`.
    if is_equal_approx(health, 0.0):
        emit_signal("health_depleted")

func set_energy(value: int) -> void:
    var energy_previous := energy
    # Energy works with whole numbers in this demo but the `clamp()` function
    # returns a floating-point value. You can let the compiler cast the value to
    # an integer, which will trigger a warning. I prefer to always do it
    # explicitly to remind myself that I'm working with integers here.
    energy = int(clamp(value, 0.0, max_energy))
    emit_signal("energy_changed", energy_previous, energy)
```

`clamp()`
> float clamp(value: float, min: float, max: float)
> Clamps value and returns a value not less than min and not more than max.
> 
	a = clamp(1000, 1, 20) # a is 20
	a = clamp(-10, 1, 20)  # a is 1
	a = clamp(15, 1, 20)   # a is 15

`emit_signal()`

> . This method supports a variable number of arguments, so parameters are passed as a comma separated list

`is_equal_approx`

> bool is_equal_approx(a: float, b: float)

> Returns true if a and b are approximately 大约  equal to each other.

> Here, approximately equal means that a and b are within a small internal  内部的 epsilon of each other, which scales with the magnitude of the numbers.

> Infinity values of the same sign are considered equal.


## Base & Final stats

```javascript
export var base_attack := 10.0 setget set_base_attack
export var base_defense := 10.0 setget set_base_defense
export var base_speed := 10.0 setget set_base_speed
export var base_hit_chance := 100.0 setget set_base_hit_chance
export var base_evasion := 0.0 setget set_base_evasion

var attack := base_attack
var defense := base_defense
var speed := base_speed
var hit_chance := base_hit_chance
var evasion := base_evasion
```

`attack` & `defense` 决定了Battler 的伤害和收到的伤害
`speed` 决定了 Battler 能多快获得攻击的机会（玩家的肯定是要比 AI 来的快，要不然还怎么打呀
`hit_chance` （可以理解喂攻击机会，中 Debuff 的时候这个会下降，更高的机会攻击会 miss 
`evasion`  就是 DOTA 里的闪避，越高敌方就越难命中

```python
var _attack := base_attack setget , get_attack
# ...

func get_attack() -> float:
    return attack
```

GDScript 里，`setget` 用来自定义 setter 和 getter 方法，但是为了避免团队里面有人不小心改动了 final stats，所以没有设置一个 setter 来更新 attack 的值，而是只设置了一个 getter 方法（只能拿到数值，但不能更改

但上面这个这是例子，在这个 project 里面并没有被应用到真实的代码里

